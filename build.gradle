group = "org.spockframework"
version = "1.0"

usePlugin "war"
usePlugin "groovy" 
sourceCompatibility = 1.5 
targetCompatibility = 1.5

def explodedWar = file("$buildDir/exploded")

repositories {
  flatDir dirs: "$rootDir/lib"
  mavenCentral()
  mavenRepo urls: "http://m2repo.spockframework.org/snapshots"
}

dependencies {
  // from Maven repos
  groovy "org.codehaus.groovy:groovy-all:1.6.7"
  compile "org.spockframework:spock-core:0.4-SNAPSHOT@jar"
  compile "junit:junit:4.7"
  
  // from lib folder
  runtime ":appengine-api-1.0-sdk:1.3.0"
  runtime ":appengine-api-labs:1.3.0"
  runtime ":gaelyk:0.3.2"
  runtime ":ocpsoft-pretty-time-1:1.0.2"
  //providedCompile "javax.servlet:servlet-api:2.5"
}

war.doLast {
  // create exploded WAR directory
  ant.unzip src: war.archivePath, dest: explodedWar
}

task wrapper(type: Wrapper, description: "Creates Gradle wrapper (rarely needed)") {
  gradleVersion = '0.8' 
  jarPath = 'wrapper' 
}

task jettyStart(description: "Starts the development application server") << {
  exec "dev_appserver.sh $explodedWar.absolutePath"
}

task jettyStop(description: "Stops the development application server") << {
  // works on command line but not here - why?
  exec """ps ww -o pid,command | awk '/com.google.appengine.tools.development.DevAppServerMain/ && !/awk/ {system("kill " \$1);}'"""  
}

task deploy(dependsOn: war, description: "Deploys application to Google App Engine") << {
  exec "appcfg.sh update $explodedWar.absolutePath"
}

def exec(command) {
  println "Executing: $command"
  def proc = command.execute()
}


